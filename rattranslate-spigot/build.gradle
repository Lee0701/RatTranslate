import org.apache.tools.ant.filters.ReplaceTokens

repositories {
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    compile project(':rattranslate-common')
    compile group: 'org.spigotmc', name: 'spigot-api', version: '1.12.2-R0.1-SNAPSHOT'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: project.version]
    }
}

shadowJar {
    classifier = ''
    dependencies {
        include dependency(':rattranslate-common')
        include dependency(':gson')
        include dependency(':guava')
    }
    relocate 'com.google', 'io.github.ranolp.rattranslate.libs'
}

artifacts {
    archives shadowJar
}

task prepareUpload(type: Copy, dependsOn: ['build']) {
    from "$buildDir/libs"
    into "$buildDir/libs"
    include "rattranslate-${project.version}.jar"
    rename "rattranslate-${project.version}.jar", 'RatTranslate.jar'
}
